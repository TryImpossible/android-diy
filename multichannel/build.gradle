plugins {
    id 'com.android.application'
    id 'kotlin-android'
}
apply plugin: 'com.didi.dokit'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.barry.multichannel"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName project.hasProperty('VERSION') ? VERSION : "1.0" // 编译传参: gradlew assembleRelease -PVERSION=x.x.x

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile file("${STORE_FILE}")
            storePassword "${STORE_PWD}"
            keyAlias "${KEY_ALIAS}"
            keyPassword "${KEY_PWD}"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.release
            buildConfigField("String", "ENV", '"development"')
        }
        release {
            //是否混淆
            minifyEnabled true
            //是否设置zip对齐优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            // 签名
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "ENV", '"producation"')
        }
        dev {
            initWith(buildTypes.debug)
            buildConfigField("String", "ENV", '"development"')
        }
        stag {
            initWith(buildTypes.release)
            buildConfigField("String", "ENV", '"staging"')
        }
        prod {
            initWith(buildTypes.release)
            buildConfigField("String", "ENV", '"producation"')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    flavorDimensions 'default'
    productFlavors {
        channel1 {
            dimension "default"
            applicationId 'com.barry.multichannel1'
            buildConfigField("String", "SERVER_URL", '"https://channel1.com/"')
            manifestPlaceholders = [
                    app_name: "channel1",
                    app_icon: "@mipmap/ic_launcher"
            ]
        }
        channel2 {
            dimension "default"
            applicationId 'com.barry.multichannel2'
            buildConfigField("String", "SERVER_URL", '"https://channel2.com/"')
            manifestPlaceholders = [
                    app_name: "channel2",
                    app_icon: "@mipmap/ic_launcher"
            ]
        }
    }
}

dokitExt {
    //通用设置
    comm {
        //地图经纬度开关
        gpsSwitch true
        //网络开关
        networkSwitch true
        //大图开关
        bigImgSwitch true
        //webView js 抓包
        webViewSwitch true
    }

    slowMethod {
        //0:默认模式 打印函数调用栈 需添加指定入口  默认为application onCreate 和attachBaseContext
        //1:普通模式 运行时打印某个函数的耗时 全局业务代码函数插入
        strategy 0
        //函数功能开关
        methodSwitch true

        //调用栈模式配置
        stackMethod {
            //默认值为 5ms 小于该值的函数在调用栈中不显示
            thresholdTime 10
            //调用栈函数入口
            enterMethods = ["com.didichuxing.doraemondemo.MainDebugActivity.test1"]
            //黑名单 粒度最小到类 暂不支持到方法
            methodBlacklist = ["com.facebook.drawee.backends.pipeline.Fresco"]
        }
        //普通模式配置
        normalMethod {
            //默认值为 500ms 小于该值的函数在运行时不会在控制台中被打印
            thresholdTime 500
            //需要针对函数插装的包名
            packageNames = ["com.didichuxing.doraemondemo"]
            //不需要针对函数插装的包名&类名
            methodBlacklist = ["com.didichuxing.doraemondemo.dokit"]
        }
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    debugImplementation 'com.didichuxing.doraemonkit:dokitx:3.3.5'
    releaseImplementation 'com.didichuxing.doraemonkit:dokitx-no-op:3.3.5'
    implementation "androidx.multidex:multidex:2.0.1"
}


//自定义copyApk task
task copyTask {
    doLast {
        //遍历
        android.productFlavors.all { flavor ->
            def fileName = "app_${name}_v${android.defaultConfig.versionName}.apk"

//        拷贝文件的始发地
            def sourceFile = "./build/outputs/apk/${name}/release/" + fileName

//        指定文件拷贝的目的地
            def destationFile = new File(project.getProjectDir().path + "/build/outputs/apks/")
            try {
//            判断文件夹是否存在
                if (!destationFile.exists()) {
                    destationFile.mkdir()
                }
                //拷贝
                copy {
                    from sourceFile
                    into destationFile

                    rename {
                        fileName
                    }
                }

            } catch (Exception e) {
                e.printStackTrace()
            }
        }
    }
}

project.afterEvaluate { project ->

//    获得build task
    def buildTask = project.tasks.getByName('build')
    if (buildTask == null) {
        throw GradleException('the build task is not found')
    }
    buildTask.doLast {
        copyTask.execute()
    }
}